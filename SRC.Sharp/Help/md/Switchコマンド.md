** 内容はSRC2.2.33のものです **
**Switchコマンド**

条件式の値にあわせて対応するコマンドを実行します。

**書式**

**Switch** *expression*

**Case** *value*

*commands*

**Case** *value*

*commands*

：

**EndSw**

**指定項目説明**

*expression*条件式

*value*対応する値

*commands*実行するイベントコマンド。複数行指定可能

**解説**

条件式*expression* の値が*value* の値に等しい**Case**文以降の*commands* を実行します。一つの**Case**文に複数の *value* を設定することもできます。

**例** Case 1 2 3

*value* に Else を指定すると、その**Case**文は*expression* の値にかかわらず実行されるようになります。**Switch**文の最後に配置し、「どの**Case**文にも該当しなかった場合」の処理に使用してください。

*expression* の値に該当する**Case**文の検索は上から順に行われます。従って複数の**Case**文が条件を満たしている場合は最も上のものが優先されます。

![](../images/bm0.gif) イベントファイルが見やすいように*commands* にはインデントをつけるようにして下さい。

**例**
```sh
#配列にデータを設定
感情[1] = 同
感情[2] = 友
感情[3] = 喜
感情[4] = 愛
感情[5] = 悲
感情[6] = 冷
感情[7] = 怒
感情[8] = 悩
感情[0] = 無
Talk アリス
ふふ、信じるも信じないも自由だけどね～
End
#5秒間の時間制限で選択肢を表示
Question 50 感情選択
同
友
喜
愛
悲
冷
怒
悩
End
#選択肢に基づいて処理を行う
Switch 感情[選択]
Case 同 友
Talk アリス
ふ～ん、いい心がけね～
End
Case 喜
Talk アリス
あら、$(主人公愛称)って余裕ね。たのもしい～
End
Case 愛
Talk アリス
うふふ～ $(主人公愛称)は大丈夫よ。私が付いてるからね～
End
Case 冷 怒
Talk アリス
あれ、$(主人公愛称)って意外に気が短いのね。長生きできないよ～
End
Case Else
Talk アリス
どうしたの？
End
EndSw
```

